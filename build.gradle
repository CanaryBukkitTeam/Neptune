buildscript {
    repositories {
        maven {
            url = 'https://plugins.gradle.org/m2'
        }
        maven {
            name = 'forge'
            url = 'http://files.minecraftforge.net/maven'
        }
        maven {
            name = 'sponge'
            url = 'http://repo.spongepowered.org/maven'
        }
    }
    dependencies {
        classpath 'net.minecraftforge.gradle:ForgeGradle:2.1-SNAPSHOT'
        classpath 'com.github.jengelman.gradle.plugins:shadow:1.2.3'
        classpath 'org.spongepowered:mixingradle:0.3-SNAPSHOT'
        classpath 'gradle.plugin.net.minecrell:licenser:0.1.2'
    }
}

apply plugin: 'net.minecraftforge.gradle.tweaker-server'
apply plugin: 'org.spongepowered.mixin'
apply plugin: 'com.github.johnrengelman.shadow'
apply plugin: 'net.minecrell.licenser'

group = 'org.neptunepowered'
archivesBaseName = project.name.toLowerCase()

sourceCompatibility = '1.8'
targetCompatibility = '1.8'

repositories {
    maven {
        name = 'vi-repo'
        url = 'http://repo.visualillusionsent.net/repository/internal'
    }
    maven {
        name = 'vi-snapshots'
        url = 'http://repo.visualillusionsent.net/repository/snapshots'
    }
    maven {
        name = 'sponge'
        url = 'http://repo.spongepowered.org/maven'
    }
    maven {
        name = 'mcstats-releases'
        url = 'http://repo.mcstats.org/content/repositories/releases'
    }
    maven {
        name = 'mcstats-snapshots'
        url = 'http://repo.mcstats.org/content/repositories/snapshots'
    }
}

configurations {
    shadow
}

ext.apiVersion = '1.2.1-SNAPSHOT'

dependencies {
    compile "net.canarymod:CanaryLib:$apiVersion"
    compile('org.spongepowered:mixin:0.5.3-SNAPSHOT') {
        exclude module: 'launchwrapper'
        exclude module: 'guava'
    }

    compile 'org.ow2.asm:asm-debug-all:5.0.4'

    shadow 'org.ow2.asm:asm-all:5.0.4'
    shadow 'net.sf.jopt-simple:jopt-simple:4.6'
}

license {
    header file('HEADER.txt')
    include '**/*.java'
    newLine = false
}

minecraft {
    version = '1.9'
    mappings = 'snapshot_20160304'
    runDir = 'run'

    tweakClass = 'org.neptunepowered.vanilla.launch.NeptuneServerTweaker'
    atSource sourceSets.main

    makeObfSourceJar = false
}

// Set project version
version = "$minecraft.version-$apiVersion"

sourceSets {
    main {
        refMap = 'mixins.vanilla.refmap.json'
    }
}

// Source compiler configuration
configure([compileJava, compileTestJava]) {
    options.compilerArgs += ['-Xlint:all', '-Xlint:-path']
    options.deprecation = true
    options.encoding = 'UTF-8'
}

test {
    testLogging {
        exceptionFormat = 'full' // Always print full stack trace if something goes wrong in the unit tests
        showStandardStreams = true
    }
}

jar {
    classifier = 'base'
    manifest {
        attributes(
                'Main-Class': 'org.neptunepowered.vanilla.launch.NeptuneServerMain',
                'Class-Path': 'minecraft_server.1.9.jar libraries/net/minecraft/launchwrapper/1.12/launchwrapper-1.12.jar'
        )
    }
}

// Reobfuscate to SRG mappings
reobf.jar.mappingType = 'SEARGE'

shadowJar {
    classifier = ''
    configurations += [project.configurations.shadow]

    // Clear the current source set
    // TODO: Find a better way for this, will likely break in the future
    mainSpec.sourcePaths.clear()

    dependsOn reobfJar

    // Wait for reobf task to be fully configured
    afterEvaluate {
        // Include obfuscated classes
        from zipTree(reobfJar.jar)
    }

    // Exclude ForgeGradle classes from shaded JAR
    exclude 'GradleStart**'
    exclude 'net/minecraftforge/**'
    exclude 'dummyThing'

    from plugins.getPlugin('net.minecraftforge.gradle.tweaker-server').delayedFile(net.minecraftforge.gradle.common.Constants.MCP_DATA_SRG)
    rename 'joined.srg', 'mappings.srg'

    dependencies {
        include dependency('net.canarymod:CanaryLib')
        include dependency('org.spongepowered:mixin')

        // CanaryLib
        include dependency('net.visualillusionsent:viutils')
        include dependency('org.jdom:jdom2')
        include dependency('com.mchange:c3p0')
        include dependency('com.googlecode.json-simple:json-simple')
        include dependency('org.mcstats.standalone:metrics')

        // Launchwrapper
        include dependency('net.sf.jopt-simple:jopt-simple')
        include dependency('org.ow2.asm:asm-all')
    }
}

task sourceJar(type: Jar, overwrite: true) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

javadoc {
    options.encoding = 'UTF-8'
    options.charSet = 'UTF-8'
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives shadowJar
    archives sourceJar
    archives javadocJar
}

task wrapper(type: Wrapper) {
    gradleVersion = '2.11'
}
